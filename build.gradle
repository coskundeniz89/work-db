buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE")
        classpath "gradle.plugin.at.comm_unity.gradle.plugins:jpamodelgen-plugin:1.1.4"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url  "http://dl.bintray.com/mental-soft/meparty"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

//Buildden önce temizle
compileJava { dependsOn += ['clean'] }

//Sadece gradle(w) komutu çalıştırıldığında çalışacak tasklar
defaultTasks 'clean', 'check', 'build', 'javadoc'


apply plugin: 'idea'
idea {
    module {
        //Uygulamayı ideada açarsanız
        //open source kütüphanelerin dokümanını ve kaynak kodunu da indirir.
        downloadSources = true
        downloadJavadoc = true
    }
}

bootRun {
    //bootRun ile çalıştırıldığında environment set edebilmek için gerekli.
    systemProperties = System.properties
    //Remote debug yapabilmek için
    jvmArgs = ["-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005"]

}

test {
    testLogging {
        // Show that tests are run in the command-line output
        events 'STARTED', 'PASSED', 'FAILED'
    }
    ignoreFailures ignoreFailuresInCheck.toBoolean()
}

apply from: "https://raw.githubusercontent.com/mental-soft/gradle-config/master/tool/findbugs.gradle"
apply from: "https://raw.githubusercontent.com/mental-soft/gradle-config/master/tool/jdepend.gradle"


apply from: "https://raw.githubusercontent.com/mental-soft/gradle-config/master/dependencies-api.gradle"


dependencies {
    // https://mvnrepository.com/artifact/org.hibernate/hibernate-jpamodelgen
    compile group: 'org.hibernate', name: 'hibernate-jpamodelgen'

}

//apply plugin: "at.comm_unity.gradle.plugins.jpamodelgen"